plugins {
    alias(libs.plugins.jte)
}

dependencies {
    implementation project(":operations")
    implementation nvaLibs.apigateway
    implementation nvaLibs.core
    implementation nvaLibs.json
    implementation nvaLibs.secrets
    implementation nvaCatalog.aws.sdk2.secrets

    implementation nvaCatalog.aws.sdk2.dynamo
    implementation nvaCatalog.aws.sdk2.dynamodbenhanced
    implementation nvaCatalog.aws.sdk2.urlconnection

    implementation nvaCatalog.bundles.jackson
    implementation nvaCatalog.guava

    implementation libs.jte

    testImplementation nvaLibs.testutils
    testImplementation nvaCatalog.assertj.core
    testImplementation nvaCatalog.bundles.testing.junit
    testImplementation nvaCatalog.bundles.testing.hamcrest
    testImplementation nvaCatalog.bundles.testing.mockito
    testImplementation nvaCatalog.aws.dynamodb.local
}

jte {
    sourceDirectory = sourceSets.main.resources.srcDirs.first().toPath().resolve('jte')
    targetDirectory = layout.buildDirectory.dir('jte-classes').get().asFile.toPath()
    contentType = gg.jte.ContentType.Html
    generate()
}

tasks.named('classes') {
    dependsOn 'generateJte'
}

sourceSets {
    main {
        java {
            srcDir(layout.buildDirectory.dir('jte-classes'))
        }
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['gg/jte/generated/**'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['gg/jte/generated/**'])
        }))
    }
}

tasks.withType(Checkstyle).configureEach {
    exclude 'gg/jte/generated/**'
}

tasks.withType(Pmd).configureEach {
    exclude 'gg/jte/generated/**'
}

test {
    environment("COGNITO_AUTHORIZER_URLS", "urls")
    environment("ALLOWED_ORIGIN", "*")
    environment("HANDLE_PREFIX", "prefix")
    environment("TABLE", "table")
}
